/*~~~>
    Thank you Phunks, your inspiration and phriendship meant the world to me and helped me through hard times.
      Never stop phighting, never surrender, always stand up for what is right and make the best of all situations towards all people.
      Phunks are phreedom phighters!
        "When the power of love overcomes the love of power the world will know peace." - Jimi Hendrix <3

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################%%%%%@@@@@((((((((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################%%%%%@@@@@((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@########################################%%%%%@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@########################################%%%%%@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@###############@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@###############@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@#PHUNKYJON///////////////#PHUNKYJON//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@#PHUNKYJON///////////////#PHUNKYJON//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////@EYES////////////////////@EYES///////////////@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////@EYES////////////////////@EYES///////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////////////////////////////////////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////////////////////////////////////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////NOSE@NOSE@////////////////////#####@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////NOSE@NOSE@////////////////////#####@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@#####//////////////////////////////##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@#####//////////////////////////////##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@###################################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@###################################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((EMBER(((((,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@MOUTH&&&&&####################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((EMBER(((((,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@MOUTH&&&&&####################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@##############################/////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@##############################/////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@///////////////@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@///////////////@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

 <~~~*/
pragma solidity  >=0.8.0 <0.9.0;

import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Receiver.sol";
import "@openzeppelin/contracts/security/Pausable.sol";

/*~~~>
Interface declarations for upgradable contracts
<~~~*/
interface NFTMkt {
    function transferNftForSale(address receiver, uint itemId) external;
}
interface RoleProvider {
  function hasTheRole(bytes32 role, address _address) external returns(bool);
  function fetchAddress(bytes32 _var) external returns(address);
}
interface Offers {
  function fetchOfferId(uint marketId) external returns(uint);
  function refundOffer(uint itemID, uint offerId) external;
}
interface Bids {
  function fetchBidId(uint marketId) external returns(uint);
  function refundBid(uint bidId) external;
}
interface Collections {
  function fetchCollection(address nftContract) external returns(bool);
}

contract MarketTrades is ReentrancyGuard, Pausable {
  using Counters for Counters.Counter;
  
  //*~~~> counter increments NFTs Trade Offers
  Counters.Counter private _trades;
  Counters.Counter private _blindTrades;

  //*~~~> Roles for designated accessibility
  bytes32 public constant PROXY_ROLE = keccak256("PROXY_ROLE");
  bytes32 public constant CONTRACT_ROLE = keccak256("CONTRACT_ROLE");
  bytes32 public constant DEV_ROLE = keccak256("DEV_ROLE");
  modifier hasAdmin(){
    require(RoleProvider(roleAdd).hasTheRole(PROXY_ROLE, msg.sender), "DOES NOT HAVE ADMIN ROLE");
    _;
  }
  modifier hasContractAdmin(){
    require(RoleProvider(roleAdd).hasTheRole(CONTRACT_ROLE, msg.sender), "DOES NOT HAVE CONTRACT ROLE");
    _;
  }
  modifier hasDevAdmin(){
    require(RoleProvider(roleAdd).hasTheRole(DEV_ROLE, msg.sender), "DOES NOT HAVE DEV ROLE");
    _;
  }

  //*~~~> Upgradable addresses
  address public roleAdd;

  //*~~~> global address variable from Role Provider contract
  bytes32 public constant MARKET = keccak256("MARKET");

  bytes32 public constant BIDS = keccak256("BIDS");

  bytes32 public constant COLLECTION = keccak256("COLLECTION");

  bytes32 public constant OFFERS = keccak256("OFFERS");

  uint[] private openStorage;
  uint[] private blindOpenStorage;

  //*~~~> Fee constructor initially set to .005%
  constructor(address _role){
    roleAdd = _role;
  }

  //*~~~> Declaring object struct for trades entered
  struct Trade {
      bool is1155; 
      uint itemId;
      uint tradeId;
      uint tokenId;
      uint amount1155;
      address nftCont;
      address payable trader;
      address seller;
  }
    struct BlindTrade {
      bool is1155;
      bool isActive;
      bool isSpecific;
      uint wantedId;
      uint tradeId;
      uint tokenId;
      uint amount1155;
      address nftCont;
      address wantCont;
      address trader;
  }

  //*~~~> Memory array of all Trades
  mapping (uint256 => Trade) private idToNftTrade;
  mapping (uint256 => BlindTrade) private idToBlindTrade;
  mapping (uint => uint) private marketIdToTradeId;

  //*~~~> Declaring event object structure for trade entered
  event TradeEntered(
      bool is1155,
      uint itemId,
      uint tradeId,
      uint tokenId,
      uint amount1155,
      address indexed nftCont,
      address indexed trader,
      address indexed seller
  );
  event BlindTradeEntered(
      bool is1155,
      bool isSpecific,
      uint wantedId,
      uint tradeId,
      uint tokenId,
      uint amount1155,
      address indexed nftCont,
      address indexed wantCont,
      address indexed trader
  );

  //*~~~> Declaring event object structure for trade withdrawn
  event TradeWithdrawn(
      bool is1155,
      uint itemId,
      uint tradeId,
      uint indexed tokenId,
      address indexed nftCont,
      address indexed trader
  );

  //*~~~> Declaring event object structure for trade accepted
  event TradeAccepted(
      bool is1155,
      bool isActive,
      uint indexed itemId,
      uint indexed tradeId,
      uint tokenId,
      address nftCont,
      address indexed trader,
      address seller
  );  

  //*~~~> Declaring event object structure for trade updated
  event TradeUpdated(
      bool is1155,
      bool isActive,
      uint indexed itemId,
      uint indexed tradeId,
      uint tokenId,
      address nftCont,
      address indexed trader,
      address seller
  );

  ///@notice
  /*~~~>
    Public function to enter a trade of an ERC721 or ERC1155 NFT for any item listed on market
  <~~~*/
  ///@dev
  /*~~~>
    amount1155: how many ERC1155 to be offered for trade;
    itemId: Market contract internal state itemId;
    tokenId: specific token Id to trade listed item for;
    nftContract: contract address of the NFT to trade;
    seller: ownerOf NFT desired;
  <~~~*/
  function enterTrade(
      uint[] memory amount1155,
      uint[] memory itemId,
      uint[] memory tokenId,
      address[] memory nftContract,
      address[] memory seller
  ) public whenNotPaused nonReentrant returns(bool){
    for (uint i;i<itemId.length;i++) {
      require(Collections(RoleProvider(roleAdd).fetchAddress(COLLECTION)).fetchCollection(nftContract[i]) == false);
      uint tradeId;
      if (openStorage.length>=1) {
        tradeId = openStorage[openStorage.length-1];
        _remove(0);
      } else {
        _trades.increment();
        tradeId = _trades.current();
      }
      if (amount1155[i]>0){
        IERC1155(nftContract[i]).safeTransferFrom(address(this), payable(msg.sender), tokenId[i], amount1155[i], "");
        marketIdToTradeId[itemId[i]] = tradeId;
        idToNftTrade[tradeId] = Trade(true, itemId[i], tradeId, tokenId[i], amount1155[i], nftContract[i], payable(msg.sender), seller[i]);
        emit TradeEntered(
          true,
          itemId[i], 
          tradeId, 
          tokenId[i],
          amount1155[i],
          nftContract[i], 
          msg.sender, 
          seller[i]);
      } else {
        transferFromERC721(nftContract[i], tokenId[i], address(this));
        approveERC721(nftContract[i], address(this), tokenId[i]);
        marketIdToTradeId[itemId[i]] = tradeId;
        idToNftTrade[tradeId] = Trade(false, itemId[i], tradeId, tokenId[i], amount1155[i], nftContract[i], payable(msg.sender), seller[i]);
        emit TradeEntered(
          false,
          itemId[i], 
          tradeId, 
          tokenId[i],
          amount1155[i],
          nftContract[i], 
          msg.sender, 
          seller[i]);
      }
    }
  return true;
  }

  ///@notice
  //*~~~> Public function to enter blind trades
  ///@dev
  /*~~~>
    is1155: (true) if ERC1155;
    isSpecific: (true) if item to trade for is specific;
    wantedId: token Id of the NFT desired;
    tokenId: token Id of the NFT to trade for wanted NFT;
    amount1155: how many 1155;
    nftContract: token address of the NFT entered to trade;
    wantContract: wanted contract address;
  <~~~*/
  function enterBlindTrade(
      bool[] memory is1155,
      bool[] memory isSpecific,
      uint[] memory wantedId,
      uint[] memory tokenId,
      uint[] memory amount1155,
      address[] memory nftContract,
      address[] memory wantContract
  ) public whenNotPaused nonReentrant{

    address collsAdd = RoleProvider(roleAdd).fetchAddress(COLLECTION);

    for (uint i;i<tokenId.length;i++) {
      uint tradeId;
      if (blindOpenStorage.length>=1) {
        tradeId = blindOpenStorage[blindOpenStorage.length-1];
        _remove(1);
      } else {
        _blindTrades.increment();
        tradeId = _blindTrades.current();
      }
      require(Collections(collsAdd).fetchCollection(nftContract[i]) == false);
      if (is1155[i]){
        IERC1155(nftContract[i]).safeTransferFrom(msg.sender, address(this), tokenId[i], amount1155[i], "");
      } else {
        transferFromERC721(nftContract[i], tokenId[i], address(this));
        approveERC721(nftContract[i], address(this), tokenId[i]);
      }
      idToBlindTrade[tradeId] = BlindTrade(is1155[i], true, isSpecific[i], wantedId[i], tradeId, tokenId[i], amount1155[i], nftContract[i], wantContract[i], payable(msg.sender));
      emit BlindTradeEntered(
          is1155[i],
          isSpecific[i],
          wantedId[i],
          tradeId, 
          tokenId[i],
          amount1155[i],
          nftContract[i], 
          wantContract[i],
          msg.sender);
      }
  }

  ///@notice
  //*~~~>Public function to withdraw trade
  ///@dev
  /*~~~>
    isBlind: (true) if trade is blind;
    itemId: Market internal storage id;
    tradeId: internal this storade id;
  <~~~*/
  ///@return Bool
  function withdrawTrade(
      bool[] memory isBlind,
      uint[] memory itemId,
      uint[] memory tradeId
  ) public nonReentrant returns(bool){
    for (uint i; i<itemId.length; i++) {
      if(isBlind[i]){
      BlindTrade memory trade = idToBlindTrade[tradeId[i]];
      require(trade.isActive == true, "Item is not listed for trade...");
      if (trade.trader != msg.sender) revert();
      if ( trade.is1155 ){
        IERC1155(trade.nftCont).safeTransferFrom(address(this), trade.trader, trade.tokenId, trade.amount1155, "");
      } else {
        transferERC721(trade.nftCont, trade.trader, trade.tokenId);
      }
      blindOpenStorage.push(tradeId[i]);
      idToBlindTrade[tradeId[i]] = BlindTrade(false, false, false, 0, trade.tradeId, 0, 0, address(0x0), address(0x0), address(0x0));
      emit TradeWithdrawn(
          false,
          itemId[i], 
          tradeId[i],  
          trade.tokenId,
          trade.nftCont, 
          trade.trader
          );
      } else {
      Trade memory trade = idToNftTrade[tradeId[i]];
      require(trade.tradeId > 0, "Item is not listed for trade...");
      if (trade.trader != msg.sender) revert();
      if ( trade.is1155 ){
        IERC1155(trade.nftCont).safeTransferFrom(address(this), payable(msg.sender), trade.tokenId, trade.amount1155, "");
      } else {
        transferERC721(trade.nftCont, trade.trader, trade.tokenId);
      }
      openStorage.push(tradeId[i]);
      marketIdToTradeId[itemId[i]] = 0;
      idToNftTrade[tradeId[i]] = Trade(false, 0, trade.tradeId, 0, 0, address(0x0), payable(0x0), address(0x0));
      
      emit TradeWithdrawn(
          false,
          itemId[i], 
          tradeId[i],  
          trade.tokenId,
          trade.nftCont, 
          trade.trader
          );
        }
      }
      return true;
  }

  ///@notice
  //*~~~>Function to refund trade if the item sells
  ///@dev
  /*~~~>
    itemId: Market item id internal storage;
    tradeId: trade item id for this internal storage;
  <~~~*/
  ///@return Bool
  function refundTrade(uint itemId, uint tradeId) public nonReentrant hasContractAdmin whenPaused returns(bool){
    Trade memory trade = idToNftTrade[tradeId];
    if ( trade.is1155 ){
      IERC1155(trade.nftCont).safeTransferFrom(address(this), trade.trader, trade.tokenId, trade.amount1155, "");
    } else {
      transferERC721(trade.nftCont, trade.trader, trade.tokenId);
    }
    idToNftTrade[tradeId] = Trade(false, 0, tradeId, 0, 0, address(0x0), payable(0x0), address(0x0));
    openStorage.push(tradeId);
    emit TradeUpdated(
       trade.is1155,
       false,
       itemId, 
       tradeId, 
       trade.tokenId,
       trade.nftCont, 
       trade.trader,
       trade.seller
      );
    return true;
  }

  ///@notice
  /*~~~>
    Public function to accept trade
  <~~~*/
  ///@notice
  /*~~~>
    itemId: Market item Id internal storage;
    tradeId: Id of trade for internal storage;
  <~~~*/
  ///@return Bool
  function acceptTrade(
      uint[] calldata itemId,
      uint[] calldata tradeId
  ) public nonReentrant returns(bool){
    
    address marketAdd = RoleProvider(roleAdd).fetchAddress(MARKET);
    address bidsAdd = RoleProvider(roleAdd).fetchAddress(BIDS);
    address offersAdd = RoleProvider(roleAdd).fetchAddress(OFFERS);

    for(uint i; i<itemId.length;i++) {
      Trade memory trade = idToNftTrade[tradeId[i]];
      require(msg.sender == trade.seller,"Not Owner");
      if ( trade.is1155 ){
        IERC1155(trade.nftCont).safeTransferFrom(address(this), trade.seller, trade.tokenId, trade.amount1155, "");
      } else {
        transferERC721(trade.nftCont, trade.seller, trade.tokenId);
      }
      /*~~~> Check for the case where there is a trade and refund it. <~~~*/
      uint offerId = Offers(offersAdd).fetchOfferId(itemId[i]);
      if (offerId > 0) {
      /*~~~> Kill offer and refund amount <~~~*/
        //*~~~> Call the contract to refund the NFT offered for trade
        Offers(offersAdd).refundOffer(itemId[i], offerId);
      }
      uint bidId = Bids(bidsAdd).fetchBidId(itemId[i]);
      if (bidId>0) {
      /*~~~> Kill bid and refund bidValue <~~~*/
        //~~~> Call the contract to refund the ETH offered for a bid
        Bids(bidsAdd).refundBid(bidId);
      }
       openStorage.push(tradeId[i]);
       marketIdToTradeId[itemId[i]] = 0;
       idToNftTrade[tradeId[i]] = Trade(
           false,
           0,
           tradeId[i],
           0,
           0,
           address(0x0),
           payable(0x0),
           address(0x0)
       );
       NFTMkt(marketAdd).transferNftForSale(trade.trader, itemId[i]);
       emit TradeAccepted(
           trade.is1155,
           false,
           itemId[i], 
           tradeId[i], 
           trade.tokenId,
           trade.nftCont,
           trade.trader, 
           trade.seller
           );
    }
    return true;
    }

  ///@notice
  /*~~~>
    Public function to accept trade
  <~~~*/
  ///@notice
  /*~~~>
    tradeId: Id of trade for internal storage;
    tokenId: Id of the token if specific;
    listedId: (0) if the item is not listed for sale on the marketplace contract;
  <~~~*/
  ///@return Bool
    function acceptBlindTrade(
      uint[] memory tradeId,
      uint[] memory tokenId,
      uint[] memory listedId
  ) public whenNotPaused nonReentrant returns(bool){
    address marketAdd = RoleProvider(roleAdd).fetchAddress(MARKET);
    for(uint i; i<tradeId.length;i++) {
      uint j = tradeId[i];
      BlindTrade memory trade = idToBlindTrade[j];
            //*~~~> Disallow random acceptances if specific
      if(trade.isSpecific){
          require(tokenId[i]==trade.wantedId,"Wrong item!");
        }
      if (trade.is1155){
        if(listedId[i]==0){
          IERC1155(trade.wantCont).safeTransferFrom(address(msg.sender), trade.trader, tokenId[i], trade.amount1155, "");
        } else {
          NFTMkt(marketAdd).transferNftForSale(trade.trader, listedId[i]);
        }
      } else {
        require(IERC721(trade.nftCont).ownerOf(tokenId[i]) == msg.sender, "Not the token owner!");
        if(listedId[i]==0){
          transferERC721(trade.nftCont, msg.sender, trade.tokenId);
        } else {
          NFTMkt(marketAdd).transferNftForSale(trade.trader, listedId[i]);
        }
      }
       blindOpenStorage.push(tradeId[i]);
       idToBlindTrade[tradeId[i]] = BlindTrade(
           false,
           false,
           false,
           0,
           tradeId[i],
           0,
           0,
           payable(0x0),
           address(0x0),
           address(0x0)
       );
       emit TradeAccepted(
           trade.is1155,
           false,
           0, 
           tradeId[i], 
           trade.tokenId,
           trade.nftCont, 
           trade.trader,
           msg.sender
           );
           
    }
    return true;
    }

  /// @notice 
    /*~~~> 
      Internal function to transferFrom ERC721 NFTs, including crypto kitties/punks
    <~~~*/
  /// @dev
    /*~~~>
      assetAddr: address of the token to be transfered;
      tokenId: Id of the token to be transfered;
      to: address of recipient;
    <~~~*/
  function transferFromERC721(address assetAddr, uint256 tokenId, address to) internal virtual {
    address kitties = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
    address punks = 0xb47e3cd837dDF8e4c57F05d70Ab865de6e193BBB;
    bytes memory data;
    if (assetAddr == kitties) {
        // Cryptokitties.
        data = abi.encodeWithSignature("transferFrom(address,address,uint256)", msg.sender, to, tokenId);
    } else if (assetAddr == punks) {
        // CryptoPunks.
        // Fix here for frontrun attack. Added in v1.0.2.
        bytes memory punkIndexToAddress = abi.encodeWithSignature("punkIndexToAddress(uint256)", tokenId);
        (bool checkSuccess, bytes memory result) = address(assetAddr).staticcall(punkIndexToAddress);
        (address nftOwner) = abi.decode(result, (address));
        require(checkSuccess && nftOwner == msg.sender, "Not the NFT owner");
        data = abi.encodeWithSignature("transferPunk(address,uint256)", msg.sender, tokenId);
    } else {
        // Default.
        // We push to the vault to avoid an unneeded transfer.
        data = abi.encodeWithSignature("safeTransferFrom(address,address,uint256)", msg.sender, to, tokenId);
    }
    (bool success, bytes memory resultData) = address(assetAddr).call(data);
    require(success, string(resultData));
  }

  /// @notice 
    /*~~~> 
      Internal function to approve ERC721 NFTs for transfer, including crypto kitties/punks
    <~~~*/
  /// @dev
    /*~~~>
      assetAddr: address of the token to be transfered;
      to: address of recipient;
      tokenId: Id of the token to be transfered;
    <~~~*/
  function approveERC721(address assetAddr, address to, uint256 tokenId) internal virtual {
    address kitties = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
    address punks = 0xb47e3cd837dDF8e4c57F05d70Ab865de6e193BBB;
    bytes memory data;
    if (assetAddr == kitties) {
        // // Cryptokitties.
        // No longer needed to approve with pushing.
        // data = abi.encodeWithSignature("approve(address,uint256)", to, tokenId);
        return;
    } else if (assetAddr == punks) {
        // CryptoPunks.
        data = abi.encodeWithSignature("offerPunkForSaleToAddress(uint256,uint256,address)", tokenId, 0, to);
    } else {
      // No longer needed to approve with pushing.
      return;
    }
    (bool success, bytes memory resultData) = address(assetAddr).call(data);
    require(success, string(resultData));
  }
  
  /// @notice 
    /*~~~> 
      Internal function to transfer ERC721 NFTs, including crypto kitties/punks
    <~~~*/
  /// @dev
    /*~~~>
      assetAddr: address of the token to be transfered;
      to: address of the recipient;
      tokenId: Id of the token to be transfered;
    <~~~*/
  function transferERC721(address assetAddr, address to, uint256 tokenId) internal virtual {
    address kitties = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
    address punks = 0xb47e3cd837dDF8e4c57F05d70Ab865de6e193BBB;
    bytes memory data;
    if (assetAddr == kitties) {
        // Changed in v1.0.4.
        data = abi.encodeWithSignature("transfer(address,uint256)", to, tokenId);
    } else if (assetAddr == punks) {
        // CryptoPunks.
        data = abi.encodeWithSignature("transferPunk(address,uint256)", to, tokenId);
    } else {
        // Default.
        data = abi.encodeWithSignature("safeTransferFrom(address,address,uint256)", address(this), to, tokenId);
    }
    (bool success, bytes memory returnData) = address(assetAddr).call(data);
    require(success, string(returnData));
  }

  ///@notice
  /* Public read functions for internal state */
  function fetchTrades() public view returns (Trade[] memory) {
    uint itemCount = _trades.current();
    uint currentIndex;
    Trade[] memory trades = new Trade[](itemCount);
    for (uint i; i < itemCount; i++) {
      if (idToNftTrade[i + 1].tradeId > 0) {
        Trade storage currentItem = idToNftTrade[i + 1];
         trades[currentIndex] = currentItem;
         currentIndex++;
      }
    }
    return trades;
  }
  function fetchUserTrades(address user) public view returns (Trade[] memory) {
    uint itemCount = _trades.current();
    uint currentIndex;
    Trade[] memory trades = new Trade[](itemCount);
    for (uint i; i < itemCount; i++) {
      if (idToNftTrade[i + 1].trader == user) {
        Trade storage currentItem = idToNftTrade[i + 1];
         trades[currentIndex] = currentItem;
         currentIndex++;
      }
    }
    return trades;
  }
  function fetchBlindTrades() public view returns (BlindTrade[] memory) {
    uint itemCount = _blindTrades.current();
    uint currentIndex;
    BlindTrade[] memory trades = new BlindTrade[](itemCount);
    for (uint i; i < itemCount; i++) {
      if (idToBlindTrade[i + 1].isActive) {
        BlindTrade storage currentItem = idToBlindTrade[i + 1];
         trades[currentIndex] = currentItem;
         currentIndex++;
      }
    }
    return trades;
  }
  function fetchUserBlindTrades(address user) public view returns (BlindTrade[] memory) {
    uint itemCount = _blindTrades.current();
    uint currentIndex;
    BlindTrade[] memory trades = new BlindTrade[](itemCount);
    for (uint i; i < itemCount; i++) {
      if (idToBlindTrade[i + 1].trader == user) {
        BlindTrade storage currentItem = idToBlindTrade[i + 1];
         trades[currentIndex] = currentItem;
         currentIndex++;
      }
    }
    return trades;
  }

  function fetchTradesById(uint itemId) public view returns (Trade[] memory) {
    uint itemCount = _trades.current();
    uint currentIndex;
    Trade[] memory trades = new Trade[](itemCount);
    for (uint i; i < itemCount; i++) {
      if (idToNftTrade[i + 1].tradeId > 0) {
          if (idToNftTrade[i + 1].itemId == itemId) {
            Trade storage currentItem = idToNftTrade[i + 1];
            trades[currentIndex] = currentItem;
            currentIndex++;
        }
      }
    }
    return trades;
  }

  function fetchTrade(uint itemId) public view returns (Trade memory item) {
    uint _id = marketIdToTradeId[itemId];
    return idToNftTrade[_id];
  }

  function fetchTradeId(uint itemId) public view returns(uint tradeId){
    uint _id = marketIdToTradeId[itemId];
    return _id;
  }

  /// @notice 
  /*~~~> 
    Internal function for removing elements from an array
    Only used for internal storage array index recycling

      In order to reduce storage array size of listed items 
        while maintaining specific enumerable bidId's, 
        any sold or removed item spots are re-used by referring to their index,
        else a new storage spot is created;

        We use the last item in the storage (length of array - 1),
        in order to pop off the item and avoid rewriting 
  <~~~*/
  function _remove(uint store) internal {
      if (store==0){
      openStorage.pop();
      } else if (store==1){
      blindOpenStorage.pop();
      }
    }

  ///@notice DEV operations for emergency functions
  function pause() public hasDevAdmin {
      _pause();
  }
  function unpause() public hasDevAdmin {
      _unpause();
  }

  ///@notice
  /*~~~> External ETH transfer forwarded to role provider contract <~~~*/
  event FundsForwarded(uint value, address _from, address _to);
  receive() external payable {
    payable(roleAdd).transfer(msg.value);
      emit FundsForwarded(msg.value, msg.sender, roleAdd);
  }
  
  function onERC1155Received(address, address, uint256, uint256, bytes memory) public virtual returns (bytes4) {
        return this.onERC1155Received.selector;
    }
  function onERC721Received(
      address, 
      address, 
      uint256, 
      bytes memory
    )external pure returns(bytes4) {
        return bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"));
  }
}